~~~ TL5 compiler - Syntax tree native nodes ~~~
module tl5-compiler


struct NativeText
  owner String text
  
  new(user SyntaxTreeNode node)
    read-new(user "")->(owner self.text)
    node.check-string(user self.text)
  
  func write()
    write(user self.text[1:self.text.length - 2])


~~~ Native include declaration in the syntax tree ~~~
class NativeInclude(SyntaxTreeFunction)
  owner NativeText path

  func inst parse-new()->(owner NativeInclude new-node)
    new-node := NativeInclude()
    new-node.parse()
  
  func inst parse()
    self.path := NativeText(user self)
  
  func dynamic write()
    write(user "\n#include <")
    self.path.write()
    write(user ">\n")


~~~ Native function declaration in the syntax tree ~~~
class NativeFunction(SyntaxTreeFunction)
  owner NativeText cname
  user ModuleMembers outer-module
  
  func inst parse-new()->(owner NativeFunction new-node)
    new-node := NativeFunction()
    new-node.parse()
  
  func inst parse()
    self.outer-module := glob.current-module
    self.parse-header(copy true)
    if glob.last-char = ' '
      self.cname := NativeText(user self)
    if self.arguments.has-error
      self.syntax-error-msg(user "error raising native function")
    if self.arguments.outputs.first?
      if self.arguments.outputs.first.next?
        self.syntax-error-msg(user "more than one output to native function")
    glob.global-module.function-map.add(user self.name, user self)
  
  func dynamic link-types()
    glob.current-module := self.outer-module
    base()
    glob.current-module := _
  
  func dynamic analyze()
    glob.current-module := self.outer-module
    base()
    glob.current-module := _
  
  func dynamic write-declaration()
    ; do nothing
  
  func dynamic write()
    ; do nothing
  
  func dynamic write-cname()
    if self.cname?
      self.cname.write()
    else
      base()


~~~ Native variable declaration in the syntax tree ~~~
class NativeVariable(SyntaxTreeVariable)
  owner NativeText cname
  
  func inst parse-new()->(owner NativeVariable new-node)
    new-node := NativeVariable(user _)
    new-node.parse-native()
  
  func inst parse-native()
    self.is-native := true
    self.parse(
        copy Access.VAR, copy false, user _, user glob.global-module)
    if glob.last-char = ' '
      self.cname := NativeText(user self)
    self.my-module := _
  
  func dynamic analyze()
    if not self.type-instance.type-data.is-primitive
      self.syntax-error(
          user "only primitive types supported for native variable, got",
          user self.type-instance.type-data.name)
    base()
  
  func dynamic write()
    ; do nothing
  
  func dynamic write-cname()
    if self.cname?
      self.cname.write()
    else
      base()


~~~ Native constant declaration in the syntax tree ~~~
class NativeConstant(SyntaxTreeConstant)
  owner NativeText cname
  
  func inst parse-new()->(owner NativeConstant new-node)
    new-node := NativeConstant(user _)
    new-node.parse-native()
  
  func inst parse-native()
    self.is-native := true
    self.parse(user glob.global-module)
    if glob.last-char = ' '
      self.cname := NativeText(user self)
    self.is-ordered := true
    self.my-module := _

  func dynamic analyze()
    ; do nothing
  
  func dynamic get-constant-value()->(var Int value, var Bool has-value)
    has-value := false

  func dynamic write()
    ; do nothing
  
  func dynamic write-cname()
    if self.cname?
      self.cname.write()
    else
      base()


~~~ Native variable declaration in the syntax tree ~~~
class NativeType(TypeData)
  owner NativeText cname

  new()
    self.set-location()
    self.is-primitive := true
  
  func inst parse-new()->(owner NativeType new-node)
    new-node := NativeType()
    new-node.parse()
  
  func inst parse()
    read-new(user " ")->(owner self.name)
    if glob.last-char = ' '
      self.cname := NativeText(user self)
    self.add-type(user glob.global-module)
  
  func dynamic analyze()
    ; do nothing
  
  func dynamic write-declaration()
    ; do nothing
  
  func dynamic write-methods-declaration()
    ; do nothing
  
  func dynamic write-methods-body()
    ; do nothing
    
  func dynamic write-global()
    ; do nothing
  
  func dynamic write()
    ; do nothing
  
  func dynamic write-cname()
    if self.cname?
      self.cname.write()
    else
      base()
