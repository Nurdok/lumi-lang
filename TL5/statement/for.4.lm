~~~ TL5 compiler - Syntax tree for loop ~~~
module tl5-compiler

~~~ For loop node in the syntax tree ~~~
class SyntaxTreeForLoop(SyntaxTreeBaseLoop)
  owner String item-name
  user SyntaxTreeVariable item
  var ExpressionValue start
  var ExpressionValue iterator
  owner VariableExpression aux-variable
  user SyntaxTreeFunction has-func
  user SyntaxTreeFunction get-func
  user SyntaxTreeFunction next-func
  
  func inst parse-new(user SyntaxTreeBlock parent)->(
      owner SyntaxTreeForLoop new-node)
    new-node := SyntaxTreeForLoop(user parent)
    new-node.parse()
  
  func inst parse()
    read-new(user " ")->(owner self.item-name)
    if glob.last-char != ' '
      self.syntax-error-c(user "expected space after index name, got")
    self.read-expect(user "in ")
    self.iterator.new(user ":", user self)
    if glob.last-char = ':'
      self.start.expression := self.iterator.expression
      self.iterator.new(user ":", user self)
    
    self.parse-block()
  
  func dynamic analyze()
    owner TypeInstance type-instance
    if self.start.parsed()
      self.analyze-expression(user self.start, user glob.type-int)
      self.analyze-expression(user self.iterator, user glob.type-int)
      glob.type-int.new-type-instance()->(owner type-instance)
    else
      self.iterator.analyze()
      if not self.iterator.result-type?
        self.syntax-error-msg(user "cannot iterate void expression")
      if self.iterator.result-type.type-data is glob.type-int
        glob.type-int.new-type-instance()->(owner type-instance)
      else-if self.iterator.result-type.type-data is glob.type-string
        glob.type-char.new-type-instance()->(owner type-instance)
      else-if self.iterator.result-type.type-data is glob.type-array
        self.iterator.result-type.parameters.first.item.copy-new()->(
            owner type-instance)
      else
        self.get-iterator-type()->(owner type-instance)
    
    if self.parent.find-variable(user self.item-name, user _)->(user self.item)?
      type-instance.check-assign-to(
          copy self.item.access,
          user self.item.type-instance,
          copy self.item.access,
          user self)
    else
      new SyntaxTreeVariable item(user self.parent)
      item.name := self.item-name
      if type-instance.type-data.is-primitive
        item.access := Access.VAR
      else
        item.access := Access.USER
      item.type-instance := type-instance
      self.item := item
      self.parent.variables.add(owner item)
    self.item-name := _
    
    if self.iterator.result-type.type-data is-not glob.type-int
      self.aux-variable := VariableExpression(user self)
      self.iterator.result-type.copy-new()->(
          owner self.aux-variable.result-type)
      self.parent.add-aux-variable(
          copy Access.USER, copy false, user self.iterator.result-type)->(
          user self.aux-variable.variable)
      string-new-copy(user self.aux-variable.variable.name)->(
          owner self.aux-variable.name)
      self.get-function().has-user-assign := true
    
    base()
  
  func inst get-iterator-type()->(owner TypeInstance type-instance)
    self.iterator.result-type.type-data.find-meth(user "has")->(
        user self.has-func)
    if not self.has-func?
      self.syntax-error(
          user "cannot iterate type with no \"has\" named method -",
          user self.iterator.result-type.type-data.name)
    if self.has-func.arguments.parameters.first.next?
      self.syntax-error(
          user "iterator \"has\" method has parameters in type",
          user self.iterator.result-type.type-data.name)
    if not self.has-func.arguments.outputs.first?
      self.syntax-error(
          user "iterator \"has\" method has no outputs in type",
          user self.iterator.result-type.type-data.name)
    if self.has-func.arguments.outputs.first.next?
      self.syntax-error(
          user "iterator \"has\" method has more than one output in type",
          user self.iterator.result-type.type-data.name)
    if self.has-func.arguments.outputs.first.item.get-type-instance().type-data is-not
        glob.type-bool
      self.syntax-error(
          user "iterator \"has\" method output is not \"Bool\" in type",
          user self.iterator.result-type.type-data.name)
    
    self.iterator.result-type.type-data.find-meth(user "get")->(
        user self.get-func)
    if not self.get-func?
      self.syntax-error(
          user "cannot iterate type with no \"get\" named method -",
          user self.iterator.result-type.type-data.name)
    if self.get-func.arguments.parameters.first.next?
      self.syntax-error(
          user "iterator \"get\" method has parameters in type",
          user self.iterator.result-type.type-data.name)
    if not self.get-func.arguments.outputs.first?
      self.syntax-error(
          user "iterator \"get\" method has no outputs in type",
          user self.iterator.result-type.type-data.name)
    if self.get-func.arguments.outputs.first.next?
      self.syntax-error(
          user "iterator \"get\" method has more than one output in type",
          user self.iterator.result-type.type-data.name)
    if self.get-func.arguments.outputs.first.item.access != Access.USER and
        self.get-func.arguments.outputs.first.item.access != Access.VAR
      self.syntax-error(
          user "iterator \"get\" method output has an illegal access in type",
          user self.iterator.result-type.type-data.name)
    
    self.get-func.arguments.outputs.first.item.get-type-instance().new-replace-params(
        user self.iterator.result-type, copy 0)->(owner type-instance)
    
    self.iterator.result-type.type-data.find-meth(user "next")->(
        user self.next-func)
    if not self.next-func?
      self.syntax-error(
          user "cannot iterate type with no \"next\" named method -",
          user self.iterator.result-type.type-data.name)
    if self.next-func.arguments.parameters.first.next?
      self.syntax-error(
          user "iterator \"next\" method has parameters in type",
          user self.iterator.result-type.type-data.name)
    if self.next-func.arguments.outputs.first?
      self.syntax-error(
          user "iterator \"next\" method has outputs in type",
          user self.iterator.result-type.type-data.name)
  
  func dynamic write()
    user TypeData iterator-type(user self.iterator.result-type.type-data)
    if self.aux-variable?
      self.write-assign(
          user self.aux-variable.variable,
          owner self.iterator.expression,
          user self.parent)
    self.write-spaces()
    if self.start.parsed()
      self.start.write-preactions()
    if not self.aux-variable?
      self.iterator.write-preactions()
    if iterator-type is glob.type-int
      self.write-int-iter()
    else-if iterator-type is glob.type-string or
        iterator-type is glob.type-array
      self.write-seq()
    else
      self.write-iter()
    if self.aux-variable?
      self.write-assign(
          user self.aux-variable.variable,
          owner EmptyExpression(),
          user self.parent)
    self.write-after-block()
  
  func inst write-assign(
      user SyntaxTreeVariable item,
      owner Expression value,
      user SyntaxTreeBlock parent)
    var SyntaxTreeExpression expression-node(user parent)
    value.code-node := expression-node
    
    new VariableExpression aux-variable(user expression-node)
    aux-variable.variable := item
    string-new-copy(user item.name)->(owner aux-variable.name)
    item.type-instance.copy-new()->(owner aux-variable.result-type)
    
    new AssignOperatorExpression assign(user expression-node, user _)
    assign.is-statement := true
    assign.left-expression := aux-variable
    glob.operator-map.find(user ":=")->(user assign.operator)
    assign.right-expression := value
    assign.safe-operand-write := false
    
    expression-node.expression.expression := assign
    expression-node.write()
  
  func inst write-int-iter()
    ; for(`item` = `start`; `item` < `iterator`; ++`item`) {
    ;   `block...`
    ; }
    write(user "for (")
    write-cname(user self.item.name)
    write(user " = ")
    if self.start.parsed()
      self.start.write()
    else
      write(user "0")
    write(user "; ")
    write-cname(user self.item.name)
    write(user " < ")
    self.iterator.write()
    write(user "; ++")
    write-cname(user self.item.name)
    write(user ")")
    
    self.block.write-block-start()
    self.write-loop-block-start()
    self.block.write-block-body()
    self.block.write-block-cleanup()
    self.block.write-block-end()
  
  func inst write-seq()
    ; {int `item`_Index; for (`item`_Index = 0; `item`_Index < `iterator`->length; ++`item`_Index) {
    ;   `item` = `aux-var`[`item`_Index];
    ;   `block...`
    ; }}
    self.aux-variable.write-check-ref()
    self.write-spaces()
    write(user "{int ")
    write-cname(user self.item.name)
    write(user "_Index; for (")
    write-cname(user self.item.name)
    write(user "_Index = 0; ")
    write-cname(user self.item.name)
    write(user "_Index < ")
    self.aux-variable.write()
    write(user "->length; ++")
    write-cname(user self.item.name)
    write(user "_Index) {\n")
    
    self.write-loop-block-start()
    
    var SyntaxTreeVariable variable(user self.block)
    string-new-concat(user self.item.name, user "_Index")->(owner variable.name)
    
    new VariableExpression index-variable(user self)
    index-variable.variable := variable
    new SliceExpression slice(user self)
    slice.line-number := self.line-number
    self.aux-variable.copy-new()->(owner slice.sequence)
    slice.sequence.code-node := variable
    slice.index := index-variable
    self.item.type-instance.copy-new()->(owner slice.result-type)
    self.parent.indentation-spaces += INDENTATION-SPACES
    self.write-assign(user self.item, owner slice, user self.block)
    self.parent.indentation-spaces -= INDENTATION-SPACES
    
    self.block.write-block-body()
    self.block.write-block-cleanup()
    
    self.write-spaces()
    write(user "}}\n")
  
  func inst write-iter()
    ; while (true) {
    ;   Bool `item`_Has = false;
    ;   `has-func`()->(`item`_Has)
    ;   if (!`item`_Has) break;
    ;   `get-func`()->(`item`)
    ;   `block...`
    ;   `next-func`()
    ; }
    write(user "while (true) {\n")
    
    self.block.write-spaces()
    write(user "Bool ")
    write-cname(user self.item.name)
    write(user "_Has = false;\n")
    
    self.write-loop-block-start()
    
    var SyntaxTreeVariable variable(user self.block)
    string-new-concat(user self.item.name, user "_Has")->(owner variable.name)
    
    new VariableExpression has-variable(user self)
    has-variable.variable := variable
    string-new-concat(user self.item.name, user "_Has")->(
        owner has-variable.name)
    glob.type-bool.new-type-instance()->(owner has-variable.result-type)
    new CallArgument has-arg
    has-arg.value := has-variable
    has-arg.is-primitive := has-arg.value.result-type.type-data.is-primitive
    self.write-iter-meth(user self.has-func, owner has-arg)
    
    self.block.write-spaces()
    write(user "if (!")
    write-cname(user self.item.name)
    write(user "_Has) break;\n")
    
    new VariableExpression item-variable(user self)
    item-variable.variable := self.item
    string-new-copy(user self.item.name)->(owner item-variable.name)
    self.item.type-instance.copy-new()->(owner item-variable.result-type)
    new CallArgument item-arg
    item-arg.value := item-variable
    item-arg.is-primitive := item-arg.value.result-type.type-data.is-primitive
    item-arg.is-dynamic := item-arg.value.result-type.type-data.is-dynamic
    item-arg.is-generic :=
        self.get-func.arguments.outputs.first.item.get-type-instance().type-data is
        glob.type-generic
    self.write-iter-meth(user self.get-func, owner item-arg)
    
    self.block.write-block-body()
    
    self.write-iter-meth(user self.next-func, owner _)
    
    self.block.write-block-cleanup()
    self.write-spaces()
    write(user "}\n")
  
  func inst write-iter-meth(
      user SyntaxTreeFunction meth, owner CallArgument output-arg)
    var SyntaxTreeExpression expression-node(user self.block)
    expression-node.line-number := self.line-number
    
    new CallArgument self-arg
    self-arg.is-primitive :=
        self.aux-variable.result-type.type-data.is-primitive
    self-arg.is-dynamic := self.aux-variable.result-type.type-data.is-dynamic
    self.aux-variable.copy-new()->(owner self-arg.value)
    self-arg.value.code-node := expression-node
    
    owner CallExpression call
    new FunctionArguments arguments
    new FunctionExpression function(
        user expression-node,
        user meth,
        owner self.aux-variable.copy-new(),
        copy 0,
        copy false,
        copy false)
    if meth.is-dynamic
      call := DynamicCallExpression(
          user expression-node,
          user function,
          owner arguments,
          user self.aux-variable.result-type,
          user self.aux-variable)
    else
      call := FunctionCallExpression(
          user expression-node,
          user function,
          owner arguments,
          user self.aux-variable.result-type)
    call.line-number := self.line-number
    call.is-statement := true
    call.arguments.parameters.add(owner self-arg)
    if output-arg?
      output-arg.is-output := true
      output-arg.code-node := expression-node
      call.arguments.outputs.add(owner output-arg)
    
    expression-node.expression.expression := call
    expression-node.write()
