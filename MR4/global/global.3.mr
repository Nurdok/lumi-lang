## MR4 compiler - Global compiler data

static Global
  ## stores all language operators
  owner SyntaxTreeRoot root
  
  ## stores all language operators
  owner NameMap{Operator} operator-map
  
  ## stores all types - language built-ins and user defined
  owner NameMap{TypeData} type-map
  
  ## the currently parsed input file object
  owner File input-file
  
  ## the currently written output file object
  owner File output-file
  
  ## holds the name of the input-file and the line number of the code currently
  ## been worked with
  user String input-file-name
  var Int line-number
  
  ## current indentation
  var Int spaces
  
  ## input reading state
  owner String input-buffer
  var Char input-end
  var Int input-spaces
  var Bool got-new-line
  var Bool save-input
  
  ## built-in types
  owner TypeData type-char
  owner TypeData type-bool
  owner TypeData type-int
  owner TypeData type-empty
  owner TypeData type-func
  owner TypeData type-string
  owner TypeData type-array
  owner TypeData type-type
  owner TypeData type-file
  owner TypeData type-sys
  
  func inst init()
    new SyntaxTreeRoot in self.root
    new String{1024} in self.input-buffer
    self.init-operator-map()
    self.init-builtin-types()
  
  func inst init-operator-map()
    new NameMap{Operator} in self.operator-map
    self.add-operator(user ":=", user "=")
    self.add-operator-copy(user "+")
    self.add-operator-copy(user "-")
    self.add-operator-copy(user "*")
    self.add-operator-copy(user "/")
    self.add-operator-copy(user "+=")
    self.add-operator-copy(user "-=")
    self.add-operator(user "=", user "==")
    self.add-operator-copy(user "!=")
    self.add-operator-copy(user ">")
    self.add-operator-copy(user "<")
    self.add-operator-copy(user ">=")
    self.add-operator-copy(user "<=")
    self.add-operator(user "not", user "!")
    self.add-operator(user "or", user "||")
    self.add-operator(user "and", user "&&")
  
  func inst add-operator(user String name, user String c-name)
    new Operator operator
    operator.init(user name, user c-name)
    self.operator-map.add(user operator.name, user operator)
  
  func inst add-operator-copy(user String name)
    self.add-operator(user name, user name)
  
  func inst init-builtin-types()
    new NameMap{TypeData} in self.type-map
    self.add-builtin-type(user "Char"):(owner self.type-char)
    self.add-builtin-type(user "Bool"):(owner self.type-bool)
    self.add-builtin-type(user "Int"):(owner self.type-int)
    self.add-builtin-type(user "Empty"):(owner self.type-empty)
    self.add-builtin-type(user "Func"):(owner self.type-func)
    self.add-builtin-type(user "String"):(owner self.type-string)
    self.add-builtin-type(user "Array"):(owner self.type-array)
    self.add-builtin-type(user "Type"):(owner self.type-type)
    self.add-builtin-type(user "File"):(owner self.type-file)
    self.add-builtin-type(user "Sys"):(owner self.type-sys)
  
  func inst add-builtin-type(user String name):(owner TypeData type-data)
    new TypeData in type-data
    string-new-copy(user name):(owner type-data.name)
    self.add-type(user type-data)
  
  func inst add-type(user TypeData type-data)
    type-data.init()
    self.type-map.add(user type-data.name, user type-data)
  
  func inst m-find-type(user String name):(user TypeData type-data)
    if not (? self.type-map.find(user name):(user type-data))
      f-syntax-error(user "unknown type", user name)


var Global glob


func f-find-type(user String name):(user TypeData type-data)
  glob.type-map.find(user name):(user type-data)
  if not (? type-data)
    f-syntax-error(user "unknown type", user name)
