## MR4 compiler - Global compiler data

static Global
  ## stores all language operators
  owner Name-map{Operator} operator-map
  
  ## stores all types - language built-ins and user defined
  owner Name-map{Type-data} type-map
  
  ## the currently parsed input file object
  owner File input-file
  
  ## the currently written output file object
  owner File output-file
  
  ## holds the name of the input-file and the line number of the code currently
  ## been worked with
  user String input-file-name
  var Int line-number
  
  ## current indentation
  var Int spaces
  
  ## built-in types
  user Type-data type-char
  user Type-data type-bool
  user Type-data type-int
  user Type-data type-func
  user Type-data type-string
  user Type-data type-array
  user Type-data type-file
  user Type-data type-sys
  
  func inst init()
    self.init-operator-map()
    self.init-builtin-types()
  
  func inst init-operator-map()
    new Name-map{Operator} in self.operator-map
    self.add-operator(user ":=", user "=")
    self.add-operator-copy(user "+")
    self.add-operator-copy(user "-")
    self.add-operator-copy(user "*")
    self.add-operator-copy(user "/")
    self.add-operator-copy(user "+=")
    self.add-operator-copy(user "-=")
    self.add-operator(user "=", user "==")
    self.add-operator-copy(user "!=")
    self.add-operator-copy(user ">")
    self.add-operator-copy(user "<")
    self.add-operator-copy(user ">=")
    self.add-operator-copy(user "<=")
    self.add-operator(user "not", user "!")
    self.add-operator(user "or", user "||")
    self.add-operator(user "and", user "&&")
  
  func inst add-operator(user String name, user String c-name)
    new Operator operator
    operator.init(user name, user c-name)
    self.operator-map.add(user operator.name, owner operator)
  
  func inst add-operator-copy(user String name)
    self.add-operator(user name, user name)
  
  func inst init-builtin-types()
    new Name-map{Type-data} in self.type-map
    self.add-type(user "Char"):(user self.type-char)
    self.add-type(user "Bool"):(user self.type-bool)
    self.add-type(user "Int"):(user self.type-int)
    self.add-type(user "Func"):(user self.type-func)
    self.add-type(user "String"):(user self.type-string)
    self.add-type(user "Array"):(user self.type-array)
    self.add-type(user "File"):(user self.type-file)
    self.add-type(user "Sys"):(user self.type-sys)
  
  func inst add-type(user String name):(user Type-data type-data)
    new Type-data in type-data
    type-data.init(owner string-new-copy(user name))
    self.type-map.add(user type-data.name, owner type-data)


var Global glob
