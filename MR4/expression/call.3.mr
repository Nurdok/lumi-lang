## MR4 compiler - Call expression

## Function call expression
class CallExpression(Expression)
  owner Expression function
  owner FunctionArguments arguments
  user Expression output
  var Bool is-function-object
  
  func inst parse-new(user String ends, user SyntaxTreeCode code-node):(
      owner Expression expression, copy Char end)
    new CallExpression call-expression
    call-expression.parse(owner expression, user code-node):(copy end)
    expression := call-expression
  
  func inst parse(owner Expression function, user SyntaxTreeCode code-node):(
      copy Char end)
    self.function := function
    self.code-node := code-node
    self.set-location()
    new FunctionArguments in self.arguments
    var CallArgumentFactory argument-factory
    self.arguments.parse(user argument-factory, copy false, user code-node):(
        copy end)
  
  func dynamic analyze()
    self.function.analyze()
    self.arguments.analyze()
    if not (? self.function.result-type)
      self.m-syntax-error-msg(user "void expression is not callable")
    if self.function.result-type.type-data != glob.type-func
      self.m-syntax-error(
          user "non callable type",
          user self.function.result-type.type-data.name)
    user FunctionArguments declaration(self.function.result-type.arguments)
    user TypeInstance result-type
    declaration.get-result-type():(copy self.access, user result-type)
    if not self.is-statement
      result-type.copy-new():(owner self.result-type)
    user TypeInstance instance-type
    var Int bases
    self.function.analyze-call(user self.arguments):(
        user instance-type, copy bases, copy self.is-function-object)
    if self.arguments.check-same-as(
        user declaration, user instance-type, copy bases)
      # add omitted output
      new CallArgument output
      output.set-location()
      output.is-output := true
      output.is-native := declaration.outputs.last.item.is-native
      output.access := declaration.outputs.last.item.access
      output.value := self.add-aux-variable(
          copy declaration.outputs.last.item.access,
          copy false,
          user result-type)
      self.output := output.value
      self.arguments.outputs.add(owner output)
    else-if ? self.result-type
      self.arguments.get-output():(user self.output)
    self.assignable := ? self.result-type
  
  func dynamic write-preactions()
    self.function.write-preactions()
    if self.is-function-object
      write(user "if (")
      self.function.write-as-top()
      write(user " == NULL) ")
      self.write-raise()
      self.code-node.write-spaces()
    self.arguments.write-preactions()
    if not self.is-statement
      self.write-func-call()
      self.code-node.write-spaces()
  
  func dynamic write()
    if self.is-statement
      self.write-func-call()
    else-if ? self.output
      self.output.write()
  
  func inst write-func-call()
    self.write-call()
    self.function.write()
    self.arguments.write-call()
    write(user " )\n")
    self.arguments.write-postactions()


## Function argument call
class CallArgument(Argument)
  owner Expression value
  user SyntaxTreeCode code-node
  var Bool is-down-cast
  var Bool is-dynamic
  var Bool is-generic
  
  func dynamic parse-value(user SyntaxTreeCode code-node):(copy Char end)
    self.code-node := code-node
    parse-new-expression(
        user ",)", user code-node):(owner self.value, copy end)
    if end != ',' and end != ')'
      self.m-syntax-error-c(user "expected \",\" or \")\", got", copy end)
  
  func dynamic analyze()
    self.value.analyze()
    if self.is-output and not self.value.assignable
      self.m-syntax-error-msg(user "non assignable call output")
  
  func dynamic check-same-type-as(
      user TypeInstance type-instance,
      user TypeInstance instance-type,
      copy Int bases)
    if self.access = ACCESS-OWNER and self.value.access != ACCESS-OWNER
      self.m-syntax-error(
          user "assigning into an owner a non-owner access",
          user glob.access-names[self.value.access])
    if self.is-output and self.access != self.value.access
      self.m-syntax-error2(
          user "assigning into access",
          user glob.access-names[self.access],
          user "invalid access",
          user glob.access-names[self.value.access])
    
    owner TypeInstance real-type
    type-instance.f-new-replace-params(user instance-type, copy bases):(
        owner real-type)
    if self.is-output
      self.is-down-cast := real-type.check-assign-to(
          user self.value.result-type, user self) > 0
      self.is-generic := type-instance.type-data = glob.type-generic
    else
      real-type.check-assign-from(user self):(owner self.value)
    self.is-dynamic := real-type.type-data.is-dynamic
    delete real-type
  
  func dynamic get-output():(user Expression output)
    output := self.value
  
  func dynamic write-preactions()
    self.value.write-preactions()
    if self.is-down-cast
      # if (`value` != NULL) RAISE(`line-num`)
      write(user "if (")
      self.value.write()
      write(user " != NULL) ")
      self.write-raise()
      self.code-node.write-spaces()
  
  func dynamic write()
    # [&(]`value`[)][, [&(]`value`_Refman[)]][, [&(]`value`_Dynamic[)]]
    if self.is-output
      if self.is-down-cast or self.is-generic
        write(user "(void*)")
      write(user "&(")
    self.value.write()
    if self.is-output
      write(user ")")
    
    if not self.value.result-type.type-data.is-primitive and not self.is-native
      write(user ", ")
      if self.is-output
        write(user "&(")
      self.value.write-refman()
      if self.is-output
        write(user ")")
    
    if self.is-dynamic and not self.is-native
      write(user ", ")
      if self.is-output
        if self.is-down-cast
          write(user "(void*)")
        write(user "&(")
      self.value.write-dynamic()
      if self.is-output
        write(user ")")
  
  func dynamic write-postactions()
    if self.access = ACCESS-OWNER and not self.is-output and
        self.value.result-type.type-data != glob.type-empty
      self.code-node.write-spaces()
      self.value.write()
      write(user " = NULL;\n")
      self.code-node.write-spaces()
      self.value.write-refman()
      write(user " = NULL;\n")


class CallArgumentFactory(ArgumentFactory)
  func dynamic m-new-argument():(owner Argument new-argument)
    new CallArgument in new-argument
