## MR4 compiler - Variable expression

## A single variable expression
class Variable-expression(Text-expression)
  user Variable variable
  
  func inst parse-new(owner String text):(owner Expression expression)
    new Variable-expression variable-expression
    variable-expression.parse(owner text)
    expression := variable-expression
  
  ## parsing `variable`
  func inst parse(owner String text)
    self.text := text
  
  func dynamic write()
    self.write-text(user "Variable")

## The `base` method expression
class Base-meth-expression(Expression)
  func inst parse-new(owner String text):(owner Expression expression)
    new Base-meth-expression in expression
    delete text
  
  func dynamic write()
    write(user "Base")


## Type name expression
class Type-expression(Text-expression)
  owner Type-instance type-value
  
  func inst parse-new(owner String text):(owner Expression expression)
    new Type-expression type-expression
    type-expression.parse(owner text)
    expression := type-expression
  
  ## parsing `Type-name`
  func inst parse(owner String text)
    self.text := text
  
  func dynamic write()
    self.write-text(user "Type")


## An instance member expression
class Member-expression(Variable-expression)
  owner Expression instance
  
  func inst parse-new(user String ends):(
      owner Expression expression, copy Char end)
    new Member-expression member-expression
    member-expression.parse(owner expression, user ends):(copy end)
    expression := member-expression
  
  ## parsing `instance.variable`
  func inst parse(owner Expression instance, user String ends):(copy Char end)
    self.instance := instance
    self.read-new-value(user ends):(owner self.text, copy end)
  
  func dynamic write()
    self.instance.write()
    self.write-text(user ".Member")
