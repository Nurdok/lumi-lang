## MR4 compiler - Syntax tree testing nodes

## Assert statement in the syntax tree
class SyntaxTreeAssert(SyntaxTreeCode)
  owner Expression tested
  
  func inst parse-new(user SyntaxTreeBlock parent):(
      copy Char end, owner SyntaxTreeAssert new-node)
    new SyntaxTreeAssert in new-node
    new-node.parse(user parent):(copy end)
  
  func inst parse(user SyntaxTreeBlock parent):(copy Char end)
    self.parent := parent
    self.set-location()
    parse-new-expression(user "", user self):(owner self.tested, copy end)
  
  func dynamic analyze()
    self.analyze-expression(user self.tested, user glob.type-bool)
  
  func dynamic write()
    # TEST_ASSERT(`line`, `tested`)
    self.write-spaces()
    self.tested.write-preactions()
    write(user "TEST_ASSERT(")
    self.write-line-num()
    write(user ", ")
    self.tested.write()
    write(user ")\n")

## Assert-error statement in the syntax tree
class SyntaxTreeAssertError(SyntaxTreeCode)
  owner Expression tested
  owner String expected-error
  
  func inst parse-new(user SyntaxTreeBlock parent):(
      copy Char end, owner SyntaxTreeAssertError new-node)
    new SyntaxTreeAssertError in new-node
    new-node.parse(user parent):(copy end)
  
  func inst parse(user SyntaxTreeBlock parent):(copy Char end)
    self.parent := parent
    self.set-location()
    parse-new-expression(user ",", user self):(owner self.tested, copy end)
    if end = ','
      read-c():(copy end)
      if end = '\n'
        self.read-line-break-spaces()
      else-if end != ' '
        self.m-syntax-error-c(user "expected space after \",\", got", copy end)
      read-new(user ""):(owner self.expected-error, copy end)
      self.check-string(user self.expected-error)
  
  func dynamic analyze()
    self.tested.analyze()
  
  func dynamic write()
    if ? self.expected-error
      self.write-spaces()
      write(user "{char* MR_expected_error_prev;\n")
      self.write-spaces()
      write(user "int MR_expected_error_trace_ignore_count_prev;\n")
      self.write-spaces()
      write(user "MR_expected_error_prev = MR_expected_error;\n")
      self.write-spaces()
      write(user "MR_expected_error_trace_ignore_count_prev =")
      write(user " MR_expected_error_trace_ignore_count;\n")
      self.write-spaces()
      write(user "MR_expected_error = ")
      write(user self.expected-error)
      write(user ";\n")
      self.write-spaces()
      write(user "MR_expected_error_trace_ignore_count =")
      write(user " MR_trace_ignore_count + 1;\n")
    self.write-spaces()
    write(user "do {\n")
    self.parent.indentation-spaces += 2
    self.write-spaces()
    write(user "++MR_trace_ignore_count;\n")
    write(user "#undef RETURN_ERROR\n")
    write(user "#define RETURN_ERROR(value) break\n")
    self.write-spaces()
    self.tested.write-preactions()
    write(user "\n#undef RETURN_ERROR\n")
    write(user "#define RETURN_ERROR(value) MR_err = value; goto MR_cleanup\n")
    self.write-spaces()
    write(user "--MR_trace_ignore_count;\n")
    if ? self.expected-error
      self.write-spaces()
      write(user "MR_expected_error_trace_ignore_count =")
      write(user " MR_expected_error_trace_ignore_count_prev;\n")
      self.write-spaces()
      write(user "MR_expected_error = MR_expected_error_prev;\n")
    self.write-spaces()
    write(user "TEST_FAIL(")
    self.write-line-num()
    write(user ", 16, \"error not raised\")\n")
    self.parent.indentation-spaces -= 2
    self.write-spaces()
    write(user "} while (false);\n")
    self.write-spaces()
    write(user "--MR_trace_ignore_count;\n")
    if ? self.expected-error
      self.write-spaces()
      write(user "MR_expected_error_trace_ignore_count =")
      write(user " MR_expected_error_trace_ignore_count_prev;\n")
      self.write-spaces()
      write(user "if (MR_expected_error == NULL) {\n")
      self.write-spaces()
      write(user "  MR_expected_error = MR_expected_error_prev;\n")
      self.write-spaces()
      write(user "  TEST_FAIL_NULL(")
      self.write-line-num()
      write(user ")\n")
      self.write-spaces()
      write(user "}\n")
      self.write-spaces()
      write(user "MR_expected_error = MR_expected_error_prev;}\n")


## Test function in the syntax tree
class SyntaxTreeTestFunction(SyntaxTreeFunction)
  func inst parse-new():(copy Char end, owner SyntaxTreeTestFunction new-node)
    new SyntaxTreeTestFunction in new-node
    new-node.parse():(copy end)
  
  func inst parse():(copy Char end)
    base.parse(user _):(copy end)
    if (? self.arguments.parameters.first) or (? self.arguments.outputs.first)
      self.m-syntax-error-msg(user "test function cannot have arguments")
    glob.test-functions.add(user self.name, user self)


## Mock function in the syntax tree
class SyntaxTreeMockFunction(SyntaxTreeFunction)
  owner String mocked-name
  owner String type-name
  
  func inst parse-new():(copy Char end, owner SyntaxTreeMockFunction new-node)
    new SyntaxTreeMockFunction in new-node
    new-node.parse():(copy end)
  
  func inst parse():(copy Char end)
    base.parse(user _):(copy end)
    var Int index
    self.name.find(user "."):(copy index)
    if index < self.name.length
      string-new-copy(user self.name[0:index]):(owner self.type-name)
      string-new-copy(user self.name[index + 1:self.name.length - index - 1]):(
          owner self.mocked-name)
      delete self.name
    else
      self.mocked-name := self.name
    if ? self.type-name and self.mocked-name.equal(user "delete")
      string-new-copy(user "MockDel"):(owner self.name)
    else
      string-new-concat(user "Mock ", user self.mocked-name):(owner self.name)
  
  func dynamic check-name()
    # do not check
  
  func dynamic analyze()
    user SyntaxTreeFunction mocked-func
    if ? self.type-name
      self.find-type(user self.type-name):(user self.parent-type)
      if self.mocked-name.equal(user "delete")
        if self.parent-type.is-delete-mocked
          self.m-syntax-error(
              user "already mocking delete of type", user self.type-name)
        self.parent-type.is-delete-mocked := true
        self.arguments.add-self-parameter(user glob.type-ref)
      else
        self.parent-type.find-meth(user self.mocked-name):(user mocked-func)
        if not (? mocked-func)
          self.m-syntax-error2(
              user "mocking unknown method",
              user self.mocked-name,
              user "of type",
              user self.type-name)
        self.arguments.add-self-parameter(user self.parent-type)
    else-if self.mocked-name.equal(user "new")
      if glob.is-new-mocked
        self.m-syntax-error-msg(user "already mocking global new")
      if ? self.arguments.parameters.first or
          not (? self.arguments.outputs.first) or
          ? self.arguments.outputs.first.next
        self.m-syntax-error-msg(
            user "mock new should have only single Bool output")
      if self.arguments.outputs.first.item.get-type-instance().type-data !=
          glob.type-bool
        self.m-syntax-error-msg(
            user "mock new should have only single Bool output")
      glob.is-new-mocked := true
    else-if self.mocked-name.equal(user "delete")
      if glob.is-delete-mocked
        self.m-syntax-error-msg(user "already mocking global delete")
      glob.is-delete-mocked := true
      if ? self.arguments.parameters.first or ? self.arguments.outputs.first
        self.m-syntax-error-msg(user "mock delete should have no arguments")
      self.arguments.add-self-parameter(user glob.type-ref)
    else
      glob.root.find-function(user self.mocked-name):(user mocked-func)
      if not (? mocked-func)
        self.m-syntax-error(
            user "mocking unknown function", user self.mocked-name)
    if ? mocked-func
      if ? mocked-func.mocker-function
        self.m-syntax-error(
            user "already mocking function", user self.mocked-name)
      self.arguments.check-same-as(user mocked-func.arguments, user _, copy 0)
      mocked-func.mocker-function := self
    base.analyze()
