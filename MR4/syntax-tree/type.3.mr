## MR4 compiler - Syntax tree type

## Type node in the syntax tree 
class SyntaxTreeType(SyntaxTreeNamespace)
  user TypeData type-data
  
  func inst parse-new():(copy Char end, owner SyntaxTreeType new-node)
    new SyntaxTreeType in new-node
    new-node.parse():(copy end)
  
  func inst parse():(copy Char end)
    self.init()
    owner String name
    read-new(user ""):(owner name, copy end)
    glob.add-user-type(owner name):(user self.type-data)
    self.indentation-spaces := 2
    self.parse-block-children(user self):(copy end)
    self.indentation-spaces := 0
  
  func dynamic parse-child(user String keyword):(copy Char end)
    if not self.parse-if-function(user keyword, user self):(copy end)
      f-syntax-error(user "unknown keyword", user keyword)
  
  func inst write-declaration()
    # typedef struct `name` `name`;
    if not (? self.type-data)
      raise
    write(user "typedef struct ")
    write-cname(user self.type-data.name)
    write(user " ")
    write-cname(user self.type-data.name)
    write(user ";\n")
  
  func dynamic write()
    # struct `name` {
    #   `variables...`
    # };
    # `functions...`
    write(user "struct ")
    write-cname(user self.type-data.name)
    write(user " {\n")
    self.indentation-spaces := 2
    self.write-children(user self.variables)
    self.indentation-spaces := 0
    write(user "};")
  
  func inst write-methods-declaration()
    self.write-functions-declaration()
  
  func inst write-dtl()
    # pass
  
  func inst write-methods-body()
    self.write-children(user self.functions)


class SyntaxTreeTypeWriter
  func dynamic write(user SyntaxTreeType type-node)
    raise

class SyntaxTreeTypeDeclarationWriter(SyntaxTreeTypeWriter)
  func dynamic write(user SyntaxTreeType type-node)
    type-node.write-declaration()

class SyntaxTreeTypeMethodsDeclarationWriter(SyntaxTreeTypeWriter)
  func dynamic write(user SyntaxTreeType type-node)
    type-node.write-methods-declaration()

class SyntaxTreeTypeDtlWriter(SyntaxTreeTypeWriter)
  func dynamic write(user SyntaxTreeType type-node)
    type-node.write-dtl()

class SyntaxTreeTypeMethodsBodyWriter(SyntaxTreeTypeWriter)
  func dynamic write(user SyntaxTreeType type-node)
    type-node.write-methods-body()
