## MR4 compiler - Syntax tree function

## Function node in the syntax tree
class SyntaxTreeFunction(SyntaxTreeBlock)
  owner List{TypeCount} aux-variables
  user TypeData parent-type
  owner String name
  owner FunctionArguments arguments
  user SyntaxTreeMockFunction mocker-function
  user SyntaxTreeFunction dynamic-base-method
  var Int dynamic-base-count
  var Bool is-dynamic
  
  func inst parse-new(user TypeData parent-type):(
      copy Char end, owner SyntaxTreeFunction new-node)
    new SyntaxTreeFunction in new-node
    new-node.parse(user parent-type):(copy end)
  
  func inst parse(user TypeData parent-type):(copy Char end)
    self.init()
    self.parent-type := parent-type
    if ? self.parent-type
      self.indentation-spaces := 2
      if self.parent-type.is-dynamic
        user String meth-type
        read-until(user " ", copy false):(user meth-type, copy end)
        if meth-type.equal(user "dynamic")
          self.is-dynamic := true
        else-if not meth-type.equal(user "inst")
          self.m-syntax-error(
              user "expected \"dynamic\" or \"inst\" method type, got",
              user meth-type)
    self.parse-header():(copy end)
    self.parse-body():(copy end)
  
  func inst init()
    self.set-location()
    new List{TypeCount} in self.aux-variables
  
  func inst parse-body():(copy Char end)
    self.indentation-spaces += 2
    self.parse-block():(copy end)
    self.indentation-spaces := 2
  
  func inst parse-header():(copy Char end)
    read-new(user "("):(owner self.name, copy end)
    self.check-name()
    if end != '('
      self.m-syntax-error-c(
          user "expected \"(\" after function name, got", copy end)
    self.parse-arguments():(copy end)
  
  func inst parse-arguments():(copy Char end)
    new FunctionArguments in self.arguments
    var DeclarationArgumentFactory argument-factory
    var SyntaxTreeCode dummy-node
    dummy-node.parent := self
    self.arguments.parse(user argument-factory, user dummy-node):(copy end)
    if ? self.parent-type
      self.arguments.add-self-parameter(
          owner self.parent-type.m-new-type-instance())
  
  func dynamic check-name()
    if not f-is-legal-name(user self.name, copy false)
      self.m-syntax-error(user "illegal function name", user self.name)
    if ? self.parent-type
      user SyntaxTreeFunction meth
      self.parent-type.find-meth(user self.name):(user meth)
      if ? meth
        self.m-syntax-error(user "redefinition of method", user self.name)
      user SyntaxTreeVariable field
      self.parent-type.find-field(user self.name):(user field)
      if ? field
        self.m-syntax-error(
            user "method name overrides field", user self.name)
    else
      if ? glob.root.find-function(user self.name)
        self.m-syntax-error(user "redefinition of function", user self.name)
      if ? glob.root.find-variable(user self.name)
        self.m-syntax-error(
            user "function name overrides global variable", user self.name)
  
  func dynamic get-function():(user SyntaxTreeFunction function)
    function := self
  
  func inst add-aux-variable(copy Int access, user TypeInstance type-instance):(
      user SyntaxTreeVariable new-variable)
    var Int index(0)
    user ListNode{TypeCount} node(self.aux-variables.first)
    do
      while ? node
      while node.item.type-data != type-instance.type-data
      node := node.next
    if ? node
      node.item.count += 1
      index := node.item.count
    else
      new TypeCount type-count
      type-count.type-data := type-instance.type-data
      self.aux-variables.add(owner type-count)
    new SyntaxTreeVariable variable
    new String{type-instance.type-data.name.length +
        21} in variable.name
    variable.name.copy(user "aux_")
    variable.name.concat(user type-instance.type-data.name)
    variable.name.append(copy '_')
    var String{16} index-str
    index.str(user index-str)
    variable.name.concat(user index-str)
    variable.parent := self
    if access = ACCESS-COPY
      variable.access := ACCESS-VAR
    else
      variable.access := access
    type-instance.copy-new():(owner variable.type-instance)
    new-variable := variable
    self.variables.add(owner variable)
  
  func dynamic find-variable(user String name):(
      user SyntaxTreeVariable variable)
    base.find-variable(user name):(user variable)
    if not (? variable)
      self.arguments.find-variable(user name):(user variable)
    if not (? variable)
      glob.root.find-variable(user name):(user variable)
  
  func dynamic link-types()
    self.arguments.link-types()
    base.link-types()
  
  func dynamic analyze()
    self.arguments.analyze()
    base.analyze()
  
  func inst m-compare(user SyntaxTreeFunction other)
    if self.is-dynamic != other.is-dynamic
      self.m-syntax-error(user "illegal dynamic in function", user self.name)
  
  ## write function body
  func dynamic write()
    # #define MR_FILE_NAME "`file-name`"
    # #define MR_FUNC_NAME "`parent-type`.`function-name`"
    # Returncode `Type`_`name`(`args`) {
    #   `block...`
    # }
    # #undef MR_FILE_NAME
    # #undef MR_FUNC_NAME
    write-pre-func(user self)
    self.write-header()
    self.write-block()
    write-post-func()
  
  func inst write-header()
    write(user "\nReturncode ")
    self.write-cname()
    self.arguments.write()
  
  func inst write-block()
    # `previous-text` {
    #   Returncode MR_err = OK;
    #   `block...`
    # MR_cleanup:
    #   return MR_err;
    # }
    self.write-block-start()
    self.write-spaces()
    write(user "Returncode MR_err = OK;\n")
    base.write()
    self.write-children(user self.code-nodes)
    write(user "MR_cleanup:\n")
    self.write-spaces()
    write(user "return MR_err;\n")
    self.write-block-end()
    
  
  func inst write-cname()
    if ? self.parent-type
      write-cname(user self.parent-type.name)
      write(user "_")
    write-cname(user self.name)
  
  func inst write-pointer()
    self.arguments.write-pointer(user self.name)
  
  func dynamic write-declaration()
    # Returncode `Type`_`name`(`args`);
    self.write-header()
    write(user ";\n")

# to be mocked in unit-tests
func write-pre-func(user SyntaxTreeFunction self)
  write(user "\n#define MR_FILE_NAME \"")
  write(user self.input-file-name)
  write(user "\"\n#define MR_FUNC_NAME \"")
  if ? self.parent-type
    write(user self.parent-type.name)
    write(user ".")
  write(user self.name)
  write(user "\"")

# to be mocked in unit-tests
func write-post-func()
    write(user "#undef MR_FILE_NAME\n")
    write(user "#undef MR_FUNC_NAME\n")


## Count occurrences of a specific type
static TypeCount
  user TypeData type-data
  var Int count


## Constructor declaration in the syntax tree
class SyntaxTreeConstructor(SyntaxTreeFunction)
  func inst parse-new(user TypeData parent-type):(
      copy Char end, owner SyntaxTreeConstructor new-node)
    new SyntaxTreeConstructor in new-node
    new-node.parse(user parent-type):(copy end)
  
  func inst parse(user TypeData parent-type):(copy Char end)
    self.init()
    self.parent-type := parent-type
    self.indentation-spaces := 2
    string-new-copy(user "new"):(owner self.name)
    self.parse-arguments():(copy end)
    self.parse-body():(copy end)
  
  func dynamic analyze()
    if ? self.arguments.outputs.first
      self.m-syntax-error-msg(user "constructor with outputs")
    base.analyze()


## Main function node in the syntax tree
class SyntaxTreeMainFunction(SyntaxTreeFunction)
  func inst parse-new():(copy Char end, owner SyntaxTreeMainFunction new-node)
    new SyntaxTreeMainFunction in new-node
    new-node.parse():(copy end)
  
  func inst parse():(copy Char end)
    self.init()
    string-new-copy(user "main"):(owner self.name)
    new FunctionArguments in self.arguments
    self.arguments.init()
    self.parse-body():(copy end)
  
  func dynamic write()
    # USER_MAIN_HEADER {
    #   `block...`
    # }
    # MAIN_FUNC
    write-pre-func(user self)
    write(user "\nUSER_MAIN_HEADER")
    self.write-block()
    write-post-func()
    write(user "\nMAIN_FUNC\n")


## Native function declaration in the syntax tree
class SyntaxTreeNativeFunction(SyntaxTreeFunction)
  func inst parse-new():(copy Char end, owner SyntaxTreeNativeFunction new-node)
    new SyntaxTreeNativeFunction in new-node
    new-node.parse():(copy end)
  
  func inst parse():(copy Char end)
    self.set-location()
    self.indentation-spaces := 2
    self.parse-header():(copy end)
    new List{SyntaxTreeCode} in self.code-nodes
  
  func dynamic write()
    # do nothing


## Function declaration argument
class DeclarationArgument(Argument)
  owner SyntaxTreeVariable variable
  
  func dynamic copy-new():(owner DeclarationArgument new-argument)
    new DeclarationArgument in new-argument
    new-argument.access := self.access
    new SyntaxTreeVariable in new-argument.variable
    new-argument.variable.access := self.variable.access
    self.variable.type-instance.copy-new():(
        owner new-argument.variable.type-instance)
    string-new-copy(user self.variable.name):(owner new-argument.variable.name)
  
  func dynamic parse-value(user SyntaxTreeCode code-node):(copy Char end)
    new SyntaxTreeVariable in self.variable
    self.variable.access := self.access
    self.variable.is-output := self.is-output
    new TypeInstance in self.variable.type-instance
    self.variable.type-instance.parse(user " ", user self, user _):(copy end)
    if end != ' '
      self.m-syntax-error-c(user "expected space after type, got", copy end)
    read-new(user ",)"):(owner self.variable.name, copy end)
  
  func dynamic link-types()
    self.variable.type-instance.link-types(user self)
  
  func dynamic analyze()
    if self.variable.type-instance.type-data.is-primitive
      if self.access != ACCESS-COPY
        self.m-syntax-error2(
            user "argument",
            user self.variable.name,
            user "access should be \"copy\" for primitive types, got",
            user glob.access-names[self.access])
    else-if self.access = ACCESS-COPY
      self.m-syntax-error2(
          user "argument",
          user self.variable.name,
          user "access should not be \"copy\" for non-primitive type",
          user self.variable.type-instance.type-data.name)
    self.variable.type-instance.analyze-lengths(user self)
  
  func dynamic get-type-instance():(user TypeInstance type-instance)
    type-instance := self.variable.type-instance
  
  func dynamic get-variable():(user SyntaxTreeVariable variable)
    variable := self.variable
  
  func dynamic write()
    if self.variable.type-instance.type-data = glob.type-func
      write(user "Returncode (*")
      if self.is-output
        write(user "*")
      write-cname(user self.variable.name)
      write(user ")")
      self.variable.type-instance.arguments.write()
      return
    # `type`[*][*] `name`
    # [, RefManager*[*] `name`_Refman]
    # [, `type`_Dynamic*[*] `name`_Dynamic]
    write-cname(user self.variable.type-instance.type-data.name)
    if self.access != ACCESS-COPY
      write(user "*")
    if self.is-output
      write(user "*")
    write(user " ")
    write-cname(user self.variable.name)
    
    if not self.variable.type-instance.type-data.is-primitive
      write(user ", RefManager*")
      if self.is-output
        write(user "*")
      write(user " ")
      write-cname(user self.variable.name)
      write(user "_Refman")
    
    if self.variable.type-instance.type-data.is-dynamic
      write(user ", ")
      write-cname(user self.variable.type-instance.type-data.name)
      write(user "_Dynamic*")
      if self.is-output
        write(user "*")
      write(user " ")
      write-cname(user self.variable.name)
      write(user "_Dynamic")


class DeclarationArgumentFactory(ArgumentFactory)
  func dynamic m-new-argument():(owner Argument new-argument)
    new DeclarationArgument in new-argument
